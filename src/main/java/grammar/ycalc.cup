import java_cup.runtime.*;
import java.util.*;
import java.io.*;

parser code {:

    public void report_error(String message, Object info) {
        StringBuilder m = new StringBuilder("Error");
        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
            if (s.left >= 0) {
                m.append(" in line "+(s.left+1));
                if (s.right >= 0)
                    m.append(", column "+(s.right+1));
            }
        }
        m.append(" : "+message);
        System.err.println(m);
    }

    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
        System.exit(1);
    }
:};

/** Terminals */

terminal           AND, OR, NOT, TRUE, FALSE; // boolean

// flow
terminal           BEGIN, END, EXIT;
terminal           IF, THEN, ELSE;
terminal           WHILE, DO; // TODO: REMOVE

terminal           PRINT, READINT, LENGTH, POSITION, READSTR, CONCATENATE, SUBSTRING;  // TODO: REMOVE (LENGTH, POSITION, CONCATENATE, SUBSTRING) CHANGE (PRINT -> WRITE, READINT -> READ, READSTR -> READ)
terminal           ASSIGN;
terminal   	       EQ, LT, LE, GT, GE, NE; // num_rel
terminal    	   STREQ, STRNOTEQ; //str_rel // TODO: REMOVE

terminal           SEMI, COMMA, PLUS, MINUS, TIMES, DIVIDE, MODE, LPAREN, RPAREN; // TODO: REMOVE (LPAREN, RPAREN)
terminal Integer   NUM; // TODO: ADD (DOUBLE)
terminal String    IDENT;
terminal String    STRING;


/** Non-terminals **/ // TODO: ADD (DOUBLE EXPRESSION)
non terminal Expression expr;
non terminal Expression num_expr;
non terminal Expression str_expr;
non terminal Expression bool_expr;

non terminal Operator num_op;
non terminal Condition num_rel;
non terminal Condition str_rel;

non terminal Instruction instruction;
non terminal InstructionList instr;

non terminal Instruction assign_stat;
non terminal Instruction if_stat;
non terminal Instruction while_stat; // TODO: REMOVE
non terminal Instruction output_stat;
non terminal Main program;

/** Precedence */
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE, MODE;

precedence left OR, AND;
precedence left NOT;

precedence left ELSE;

start with program;

/** ============= GRAMMAR ============= */

/** done */
num_op ::= PLUS             {: RESULT = new PlusOperator(); :}
         | MINUS            {: RESULT = new MinusOperator(); :}
         | TIMES            {: RESULT = new TimesOperator(); :}
         | DIVIDE           {: RESULT = new DivideOperator(); :}
         | MODE             {: RESULT = new ModeOperator(); :}
         ;
// TODO: ADD DOUBLE OPS (THAT DO NOT MATCH WITH INTEGER OPS)
expr ::= IDENT:i            {: RESULT = new ID(i); :}
            |num_expr:e     {: RESULT = e; :}
            |str_expr:e     {: RESULT = e; :}
            ;
// TODO: CHANGE READINT -> READ, READSTR -> READ
// TODO: REMOVE LPAREN, RPAREN, POSITION
num_expr ::=  NUM:e
                {:
                    RESULT = new IntExpression(e.intValue());
                :}
            | READINT
                 {:
                    RESULT = new ReadExpression();
                 :}
            | MINUS expr:e
                {:
                    RESULT = new UMinusExpression(e);
                :}
            | LPAREN expr:e RPAREN
                {:
                    RESULT = new PIntExpression(e);
                :}
            | LENGTH LPAREN expr:e RPAREN
                {:
                    RESULT = new StringLengthExpression(e);
                :}
            | POSITION LPAREN expr:s COMMA expr:s2 RPAREN
                {:
                    RESULT = new StringPositionExpression(s, s2);
                :}
            | expr:e num_op:o expr:e2
                {:
                    RESULT = new OperatorExpression(e, o, e2);
                :}
            ;

str_expr ::=  STRING:s
                {:
                    RESULT = new StringExpression(s.substring(1, s.length()-1));
                :}
             | READSTR
                {:
                    RESULT = new ReadExpression();
                :}
            | CONCATENATE LPAREN expr:s COMMA expr:s2 RPAREN
                {:
                    RESULT = new ConcatStringExpression(s, s2);
                :}
              | SUBSTRING LPAREN expr:s COMMA expr:pos COMMA expr:length RPAREN
                {:
                    RESULT = new SubStringExpression(s, pos, length);
                :}
             ;

num_rel ::=   EQ         {: RESULT = new EqualsCondition(); :}
            | LT         {: RESULT = new LessThanCondition(); :}
            | LE         {: RESULT = new LessOrEqualsCondition(); :}
            | GT         {: RESULT = new GreaterThanCondition(); :}
            | GE         {: RESULT = new GreaterOrEqualsCondition(); :}
            | NE         {: RESULT = new NotEqualsCondition(); :}
            ;

str_rel ::= STREQ     {: RESULT = new StringEqualsCondition(); :}
          | STRNOTEQ  {: RESULT = new StringNotEqualsCondition(); :}
          ;

bool_expr ::= TRUE                               {: RESULT = new BooleanExpression(true); :}
            | FALSE                              {: RESULT = new BooleanExpression(false); :}
            | LPAREN bool_expr:e RPAREN          {: RESULT = new PBooleanExpression(e); :}
            | NOT bool_expr:e                    {: RESULT = new NegationBooleanExpression(e); :}
            | bool_expr:e AND bool_expr:e2       {: RESULT = new AndBooleanExpression(e, e2); :}
            | bool_expr:e OR bool_expr:e2        {: RESULT = new OrBooleanExpression(e, e2); :}
            | expr: e num_rel:o expr:e2          {: RESULT = new ConditionBooleanExpression(e, o, e2); :}
            | expr:s str_rel:o expr:s2           {: RESULT = new ConditionBooleanExpression(s, o, s2); :}
            ;

instruction ::= assign_stat:si                  {: RESULT = si; :}
              | if_stat:si                       {: RESULT = si; :}
              | while_stat:si                    {: RESULT = si; :}
              | BEGIN instr:si END               {: RESULT = new BeginEndInstruction(si); :}
              | output_stat:si                   {: RESULT = si; :}
              | EXIT                             {: System.exit(0); :}
              ;
// TODO: CHANGE TO \n
instr ::= instr:i SEMI instruction:si           {: i.add(si); RESULT = i; :}
        | instruction:si                        {: RESULT = new InstructionList(si); :}
        ;

assign_stat ::= IDENT:i ASSIGN expr:e          {: RESULT = new AssignInstruction(i,e); :}
                ;

if_stat ::= IF bool_expr:c THEN instruction:s                          {: RESULT = new IfInstruction(c, s); :}
            | IF bool_expr:c THEN instruction:s ELSE instruction:s2   {:  RESULT = new IfElseInstruction(c, s, s2); :}
            ;
// TODO: REMOVE
while_stat ::= WHILE bool_expr:c DO instruction:s       {: RESULT = new WhileInstruction(c, s); :}
            | DO instruction:s WHILE bool_expr:c        {: RESULT = new DoWhileInstruction(c, s); :}
            ;

output_stat ::= PRINT LPAREN expr:e  RPAREN               {: RESULT = new OutputInstruction(e); :}
            ;

program ::= instr:i
          {:
            Main program = new Main(i);
            program.exec();
          :}
          ;